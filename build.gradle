plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT' apply false
    id 'maven-publish'
}

var accessWidenerFile = file("space-factory-core/src/main/resources/spacefactory.accesswidener")

allprojects {
    apply plugin: "fabric-loom"

    version = project.mod_version
    group = project.maven_group

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        maven {
            url = "https://maven.terraformersmc.com/releases"
        }
    }

    dependencies {
        // To change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
        modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    }

    processResources {
        inputs.property "version", project.version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    loom {
        accessWidenerPath = accessWidenerFile
        shareCaches()
    }

    java {
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 17
    }

}

repositories {
    maven {
        name = "REI"
        url = "https://maven.shedaniel.me/"
    }
}

dependencies {
    modImplementation("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    implementation project(path: ":space-factory-core", configuration: "namedElements")
    include project(path: ":space-factory-core")
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}